test verifier

function %average(i32, i32) -> f32 system_v {
    ss1 = explicit_slot 8         ; Stack slot for ``sum``.

ebb1(v1: i32, v2: i32):
    v3 = f64const 0x0.0
    stack_store v3, ss1
    brz v2, ebb3                  ; Handle count == 0.
    v4 = iconst.i32 0
    jump ebb2(v4)

ebb2(v5: i32):
    v6 = imul_imm v5, 4
    v7 = iadd v1, v6
    v8 = load.f32 v7              ; array[i]
    v9 = fpromote.f64 v8
    v10 = stack_load.f64 ss1
    v11 = fadd v9, v10
    stack_store v11, ss1
    v12 = iadd_imm v5, 1
    v13 = icmp ult v12, v2
    brnz v13, ebb2(v12)           ; Loop backedge.
    v14 = stack_load.f64 ss1
    v15 = fcvt_from_uint.f64 v2
    v16 = fdiv v14, v15
    v17 = fdemote.f32 v16
    return v17

ebb3:
    v100 = f32const +NaN
    return v100
}
